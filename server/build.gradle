plugins {
    id 'application'
    id 'com.google.protobuf' version '0.8.14'
}

group 'real-time'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

def grpcVersion = '1.36.0'
def protobufVersion = '3.12.0'

dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-services:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    
    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"
    
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protobufVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

startScripts.enabled = false

task server(type: CreateStartScripts) {
    mainClassName = 'com.sensor.App'
    applicationName = 'server'
    classpath = startScripts.classpath
    outputDir = startScripts.outputDir
}

applicationDistribution.into("bin") {
    duplicatesStrategy= DuplicatesStrategy.EXCLUDE
    from(server)
    fileMode = 0755
}